/*
 * Licensed to Elasticsearch under one or more contributor
 * license agreements. See the NOTICE file distributed with
 * this work for additional information regarding copyright
 * ownership. Elasticsearch licenses this file to you under
 * the Apache License, Version 2.0 (the "License"); you may
 * not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

apply plugin: 'elasticsearch.testclusters'
apply plugin: 'elasticsearch.build'
apply plugin: 'elasticsearch.rest-test'

def restCompatAPIRoot =  new File(new File(new File(project.buildDir, 'resources'), 'test'), 'restCompatAPIRoot')
def restCompatTestRoot = new File(new File(new File(project.buildDir, 'resources'), 'test'), 'restCompatTestRoot');

task copyAPIFromBwc(type: Copy) {
  dependsOn ':distribution:bwc:minor:checkoutBwcBranch'
  from (project(':distribution:bwc:minor').checkoutDir){
    include 'rest-api-spec/**/rest-api-spec/api/**/*.json'
  }
  into restCompatAPIRoot
  includeEmptyDirs = false
}

task copyTestsFromBwc(type: Copy) {
  dependsOn ':distribution:bwc:minor:checkoutBwcBranch'
  from (project(':distribution:bwc:minor').checkoutDir){
    include 'rest-api-spec/**/rest-api-spec/test/**/*.yml'
    include 'modules/**/rest-api-spec/test/**/*.yml'
    include 'plugins/**/rest-api-spec/test/**/*.yml'
  }
  into restCompatTestRoot
  includeEmptyDirs = false
}

task copyFromBwc {
  dependsOn copyAPIFromBwc
  dependsOn copyTestsFromBwc
}

integTest.runner {
  dependsOn copyFromBwc
  systemProperty 'restCompatAPIRoot', restCompatAPIRoot
  systemProperty 'restCompatTestRoot', restCompatTestRoot
}

dependencies {
  testCompile project(':test:framework')
}
